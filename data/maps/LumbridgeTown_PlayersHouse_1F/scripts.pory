mapscripts LumbridgeTown_PlayersHouse_1F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_LUMBRIDGEPLAYER_STATE, 0: LumbridgeTown_PlayersHouse_1F_Frame_0
    ]
}

script LumbridgeTown_PlayersHouse_1F_Frame_0 {
    setrespawn(HEAL_LOCATION_LUMBRIDGE_TOWN_PLAYERS_HOUSE_2F)
    lock
	playse(SE_PIN)
    applymovement(1, LumbridgeTown_PlayersHouse_1F_Mom_InitMove)
    waitmovement(1)
    msgbox(format("Hello, dear!\pDid you hear the news?\lYour Trainer ID arrived today!"))
    msgbox(format("With your Trainer ID, you'll be one step closer to officially being a Pokémon Trainer!"))
    msgbox(format("Once you become an official Pokémon Trainer, you'll be able to take on the Azoria Gym Challenge."))
    msgbox(format("Isn't that great?\pI'm so glad we moved here!\pYour father found new work as a feller and now you get to take on the Azoria Gym Challenge and become a Pokémon Trainer!"))
    msgbox(format("Anyway…"))
    msgbox(format("Your Trainer ID was delivered to Professor Larch's laboratory.\pWhy don't you go by to pick it up?\pYou can also say hello to your father on the way!\lHe's working in the field."))
    closemessage
    applymovement(1, Lumbridgetown_PlayersHouse_1F_Mom_Return)
    waitmovement(1)
    setvar(VAR_LUMBRIDGEPLAYER_STATE, 1)
    release
    end
}

script LumbridgeTown_PlayersHouse_1F_Mom {
    lock
    switch (var(VAR_LUMBRIDGEPLAYER_STATE)){
        case 1:
            faceplayer
            msgbox(format("Your Trainer ID was delivered to Professor Larch's laboratory."))
            release
        case 2:
            faceplayer
            msgbox(format("Look at you! An official Pokémon Trainer…\pWow, Professor Larch even gave you a Pokémon?"))
            release
        case 4:
            faceplayer
            if (flag(FLAG_SAID_BYE_MOM) == TRUE)
            {
                msgbox(format("You're going to do great, honey!"))
            } else {
                msgbox(format("You're ready to set off on your Pokémon journey?\p{PLAYER}, you're going to do so well in the Gym Challenge.\pI can't wait to see you excel."))
                setflag(FLAG_SAID_BYE_MOM)
                if (flag(FLAG_SAID_BYE_DAD) == TRUE)
                {
                    setvar(VAR_LUMBRIDGEPLAYER_STATE, 5)
                }
            }
            release
        case 5:
            faceplayer
            msgbox(format("How are you doing, {PLAYER}?\nYou look a little tired.\pI think you should rest a bit."))
            closemessage
            call(Common_EventScript_OutOfCenterPartyHeal)
            incrementgamestat(GAME_STAT_RESTED_AT_HOME)
            msgbox(format("All better!"))
            release
    }
    end
}

script LumbridgeTown_PlayersHouse_1F_TV {
    lock
    msgbox(format("There's a show on TV.\pIt looks like two agents are solving a mystery.\pWhat's that little green thing?"))
    release
    end
}

script LumbridgeTown_PlayersHouse_1F_Sink {
    lock
    msgbox(format("The sink is squeaky clean!\pI should hope so. I just cleaned it last night."))
    release
    end
}

script LumbridgeTown_PlayersHouse_1F_Refrigerator {
    lock
    msgbox(format("Dad just brought in groceries. Nice!"))
    release
    end
}

script LumbridgeTown_PlayersHouse_1F_Display {
    lock
    msgbox(format("Mom likes to display all of her mugs here."))
    release
    end
}

script LumbridgeTown_PlayersHouse_1F_Phone {
    lock
    msgbox(format("I don't have time to use the phone right now."))
    release
    end
}

movement LumbridgeTown_PlayersHouse_1F_Mom_InitMove {
    face_player
    emote_exclamation_mark
    delay_16
    delay_16
    delay_16
    delay_16
    walk_up * 2
    walk_right
    step_end
}

movement Lumbridgetown_PlayersHouse_1F_Mom_Return {
    walk_left
    walk_down * 2
    face_left
    step_end
}